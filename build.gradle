plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "luisrjaeger.auto-versioning" version "1.0.3"
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
}

description 'Gradle plugin that create tasks to post messages on Slack WebHook'
group 'br.com.luisrjaeger'
version autoVersion.versionName()
ext.pluginId = 'luisrjaeger.slack-messaging'

autoVersion {
    versionCycle = 100
    releaseNotesFileName = null
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.google.code.gson:gson:2.8.5'
}

gradlePlugin {
    plugins {
        slackMessagingPlugin {
            id = project.pluginId
            implementationClass = project.group + '.slack.SlackMessagingPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/luisrjaeger'
    vcsUrl = 'https://github.com/luisrjaeger/slack-messaging'
    description = project.description
    tags = ['slack', 'messaging', 'webhook']
    plugins {
        slackMessagingPlugin {
            displayName = 'Slack Messaging'
        }
    }
}

publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
    repositories {
        maven {
            url mavenRepoUrl
        }
    }
}